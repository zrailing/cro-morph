Alphabet
 a á e é i í o ó u ú b c d h k m n p s t x l
 A Á E É I Í O Ó U Ú B C D H K M N P S T X L
	%{A%}:aa %{A%}:li
	%{LTM%}:a %{LTM%}:0
	%{plu%}:u 
	%{plu%}:o
	%{plu%}:0
	%{cite%}:e %{cite%}:a %{cite%}:0
	%{¬%}:0
	%{∧%}:0
	%{∨%}:0
	aa:0
	%>:0
;

Sets

ShortVow = a á i í u ú ;

LongVow = e é o ó ;

Cns = b c d h k m n p s t x ;

Nasal = m n ;

VoicelessObs = p t k ;

VoicedDent = d n l ;

Accent = á é í ó ú ; 

Rules

"VH Approximative - Short Vow"
%{A%}:aa <=> ShortVow: %>: _ ;

!! shiipi -> shiipi<n><approx>

"VH Approximative - Long Vow"
%{A%}:li <=> LongVow: %>: _;

"LTM Nom SVD"
%{LTM%}:0 <=> _ %>: [ShortVow|LongVow] ;

"Plurals - (i a u) -> uu Pt 1"
! Vx turns to u before %{plu%}, subsequent rule turns %{plu%} to u if not preceding underlying u 
Vx:u <=> _ %>: %{plu%} #;
	where Vx in ( i a u á í ú) ;
!@ s:s h:h í:í i:i p:i i:u >:0 %{plu%}:%{plu%}

"Plurals - ( i a u ) -> uu Pt 2"
! %{plu%} realizes as u if not following underlying u 
%{plu%}:u <=> u %>: _ # ; 
	except 
		[u: | ú: ]+ %>: _ # ; 
!@ s:s h:h í:í i:i p:p i:u >:0 %{plu%}:u 

"Plurals - append o "
%{plu%}:o <=> Vx Vy %>: _ # ; 
	where Vx in ( i e e u)
	      Vy in ( i e 0 u)
	matched;
!@ b:b i:i l:l á:á a:a n:n n:n e:e e:e >:0 %{plu%}:o 

"Plurals - append u "
%{plu%}:u <=> Vx Vy %>: _ # ; 
	where Vx in (a a á o o ó o)
	      Vy in (a á a o 0 o ó)
	matched;
!@ a:a k:k b:b i:i n:n n:n a:a w:w ó:ó o:o >:0 %{plu%}:u

"Plurals - a -> o"
a:o <=> Vx _ %>: %{plu%} # ; 
	where Vx in (i í u ú) ; 
!@ a:a p:p á:á a:a l:l i:i i:i a:a >:0 %{plu%}:%{plu%}

"Plurals - a -> o Pt 2"
%{plu%}:0 <=> Vx a %>: _ # ;
	where Vx in (i í u ú) ; 
!@ a:a p:p á:á a:a l:l i:i i:i a:a >:0 %{plu%}:0

"Citation forms - basic"
%{cite%}:0 <=> _ # ;
	except
		[ i | a | e | é | á | í ] %>: _ # ;
		Vx Vy %>: _ # ;  
	where Vx in (i i í u ú u)
	      Vy in (i í i u u ú)
	matched;
!@ í:í i:i l:l a:a a:a l:l e:e e:e %{cite%}:0
		

"Citation forms - e pt 1"
Vx:0 <=> _ %>: %{cite%}: # ;
!	where Vx in ( a á e é i í ) ;
!@ b:b a:a a:a i:i h:h u:u l:l í:í s:s h:h o:o o:o p:p á:0 >:0 %{cite%}:e

"Citation forms - e pt 2"
%{cite%}:e <=> [i: | a: | e: | é: | á: | í: ]  %>: _ # ; 
!@ a:a p:p á:0 >:0 %{cite%}:e

"Citation forms - o "
Vx:Vy <=> _ %>: %{cite%}: # ; 
	where Vx in ( ú u ) 
	      Vy in ( ó o )
	matched ; 
!@ c:c h:h i:i a:a x:x x:x ú:o >:0 %{cite%}:0

"Citation forms - append a "
%{cite%}:a <=> Vx Vy %>: _ # ; 
	where Vx in (i i í u ú u)
	      Vy in (i í i u u ú)
	matched; 
!@ b:b í:í t:t c:c h:h i:i i:i >:0 %{cite%}:a

!"Elimination of identical vowels"
!a:0 <=> _ [ :0 ]+ %>: [ á | a a ] ;


"Palatal-dental alternation (ch->t) Part 1"
!t occurs before a-initial suffixes and plural uu
c:t <=> _ h: ?* %>: [%{plu%}: | a | b | d] ;

!@ d:d ú:ú t:t c:t h:h i:i >:0 u:u u:u 

"Palatal-dental alternation (ch->t, sh->s) Part 2"
h:0 <=> [c: | s] _ [ShortVow | LongVow] %>: [%{plu%}: | a | b | d] ;

!@ d:d ú:ú t:t c:t h:0 i:i >:0 u:u u:u
!@ a:a l:l í:í i:i s:s h:0 i:i >:0 u:u u:u


"Tone spreading leftwards"
Vx:Vy <=> Accent ?* _ ; 
   except 
        [ :Cns :Cns ] _ %>: :Cns ;
                :Cns  _ %>: [ :Cns :Cns ] ;
               :Nasal _ %>: :VoicelessObs ; 
   where Vx in ( á é í ó ú )
         Vy in ( a e i o u )
   matched ;

!@ d:d í:í s:s >:0 b:b a:a a:a p:p á:a a:a x:x e:e

"Stem short vowel deletion"
Vx:0 <=> _ %>: ;  
   except 
        [ :Cns :Cns ] _ %>: :Cns ;
                :Cns  _ %>: [ :Cns :Cns ] ;
               :Nasal _ %>: :VoicelessObs ;
		_ %>: %{cite%}: ; 
	[ i | í | u | ú] _ %>: a ; 
   where Vx in ( a á i í u ú ) ;

"Stem ablaut in long vowels (1)"
Vx:Vy <=> _ Vx:Vy %{¬%}: %>: %{plu%}: ;
   where Vx in ( i e e ) 
              Vy in ( a i a )  
   matched ; 

"Stem ablaut in long vowels (2)"
Vx:Vy <=> Vx:Vy _ %{¬%}: %>: %{plu%}: ;
   where Vx in ( i e e ) 
              Vy in ( a i a )  
   matched ; 

!@ d:d í:í s:s >:0 í:i i:i l:l a:a a:a l:l e:e e:e >:0 o:o

"Nasal assimilation"
Cx:m <=> _ %>: [ m | b ] ;
   where Cx in ( d n l ) ;

"Final Schwa Deletion"
!The final schwa of a dipthong is deleted before suffixes beginning with a and before the plural
a:0 <=> [ i | í | u | ú] _ %>: [a | %{plu%}: ];
!@ d:d ú:ú u:u c:c h:h i:i l:l u:u a:0 >:0 plu:o
! Note that following plural morpheme is conditioned by the remaining V
